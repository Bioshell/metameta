rule dudes_run_1:
	input:
		fq1="{sample}/reads/dudes.1.fq",
		dbcheck=config['dbdir'] + "{database}/dudes_db_check.done"
	output: "{sample}/dudes/{database}/{sample}.sam"
	log: "{sample}/log/{database}/dudes_run_1.log"
	benchmark: "{sample}/log/{database}/dudes_run_1.time"
	threads: config["threads"]
	conda: srcdir("../envs/dudes.yaml")
	params: reads_cmd = lambda wildcards: "-1 " + wildcards.sample + "/reads/dudes.1.fq -2 " + wildcards.sample + "/reads/dudes.2.fq" if "fq2" in config["samples"][wildcards.sample] else "-U " + wildcards.sample + "/reads/dudes.1.fq"
	shell: "{config[tool_alt_path][bowtie2]}bowtie2 -x {config[dbdir]}{wildcards.database}/dudes_db/dudes_db -p {threads}  --no-unal --very-fast -k 10 {params.reads_cmd} -S {output} > {log} 2>&1"

rule dudes_run_2:
	input: "{sample}/dudes/{database}/{sample}.sam"
	output: "{sample}/dudes/{database}/{sample}.results.out"
	log: "{sample}/log/{database}/dudes_run_2.log"
	benchmark: "{sample}/log/{database}/dudes_run_2.time"
	threads: config["threads"]
	conda: srcdir("../envs/dudes.yaml")
	shell: "{config[tool_alt_path][dudes]}DUDes.py -s {input} -d {config[dbdir]}{wildcards.database}/dudes_db/dudes_db.npz -t {threads} -a 0.00001 -o {wildcards.sample}/dudes/{wildcards.database}/{wildcards.sample}.results > {log} 2>&1"

rule dudes_rpt:
	input: "{sample}/dudes/{database}/{sample}.results.out"
	output:	"{sample}/profiles/{database}/dudes.profile.out"
	log: "{sample}/log/{database}/dudes_rpt.log"
	benchmark: "{sample}/log/{database}/dudes_rpt.time"
	shell: "cp {input} {output} > {log} 2>&1"
