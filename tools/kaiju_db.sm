rule kaiju_db_1:
	output: db_path['kaiju'] + "kaiju_db.fmi"
			, db_path['kaiju'] + "names.dmp"
			, db_path['kaiju'] + "nodes.dmp"
			, db_path['kaiju'] + "genomes/"
	log: config['dbdir'] + "log/kaiju_db_1.log"
	benchmark: config['dbdir'] + "log/kaiju_db_1.time"
	threads: config["threads"]
	conda: srcdir("../envs/kaiju.yaml")
	shell: 
		"""
		mkdir -p {db_path[kaiju]}
		cd {db_path[kaiju]}
		{tool_path[kaiju]}makeDB.sh -t {threads} > {log} 2>&1
		"""

rule kaiju_db_profile:
	input: db_path['kaiju'] + "genomes/"
	output: config['dbdir'] + "kaiju.dbaccession.out"
	log: config['dbdir'] + "log/kaiju_db_profile.log"
	benchmark: config['dbdir'] + "log/kaiju_db_profile.time"
	shell: "zcat {input}/*.gz | grep '^VERSION' | tr -s ' ' | cut -d ' ' -f 2 > {output} 2> {log}"
	
rule kaiju_db_check:
	input: config['dbdir'] + "kaiju.dbaccession.out"
			, db_path['kaiju'] + "kaiju_db.fmi"
			, db_path['kaiju'] + "names.dmp"
			, db_path['kaiju'] + "nodes.dmp"
	output: touch(config['dbdir'] + "kaiju_db_check.done")
	log: config['dbdir'] + "log/kaiju_db_check.log"
	benchmark: config['dbdir'] + "log/kaiju_db_check.time"
	run: 
		if not config['keepfiles']: 
			shell(rmTempFilesDB(db_path["kaiju"], input[1:]) + " > {log} 2>&1")
			shell(rmEmptyFolderDB(db_path["kaiju"]) + " >> {log} 2>&1")
